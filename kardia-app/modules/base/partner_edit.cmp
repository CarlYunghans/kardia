$Version=2$
p_partner_component "widget/component-decl"
    {
    x=0;y=0;
    width=828;height=584;

    // If needing to pre-load a particular partner record, use this parameter.
    id "widget/parameter" { type=string; default=null; deploy_to_client=yes; }

    // If a finance ledger context is available...
    ledger "widget/parameter" { type=string; default=null; deploy_to_client=yes; }

    // If pre-loading a record, trigger the osrc here.
    preload_primary_partner "widget/connector"
	{
	event_condition=runclient(char_length(:p_partner_component:id) > 0);
	event=LoadComplete;
	target=primary_partner_osrc;
	action=QueryParam;
	p_partner_key=runclient(:p_partner_component:id);
	}

    // If not pre-loading, open up the search window
    open_search_first "widget/connector"
	{
	event_condition=runclient(not (char_length(:p_partner_component:id) > 0));
	event=LoadComplete;
	target=partner_search_window;
	action=Open;
	}

    // This osrc holds the record for the "primary partner" being displayed
    primary_partner_osrc "widget/osrc"
	{
	sql = runserver("
		SELECT
			*
		FROM
			/apps/kardia/data/Kardia_DB/p_partner/rows
		ORDER BY
			:p_partner_key
		");
        baseobj = "/apps/kardia/data/Kardia_DB/p_partner/rows";
	replicasize = 10;
	readahead = 5;
	autoquery = never;
	}

    p_partner_osrc "widget/osrc"
        {
        sql="	SELECT
			:p_partner_key, :p_creating_office, :p_parent_key, :p_partner_class, :p_status_code, :p_status_change_date,
			:p_title, :p_given_name, :p_preferred_name, :p_surname, :p_surname_first, :p_localized_name, :p_org_name, :p_gender,
			:p_language_code, :p_acquisition_code, :p_comments, :p_record_status_code, :p_no_solicitations, :p_no_mail,
			:p_cost_center, :p_best_contact, :p_merged_with, :p_legacy_key_1, :p_legacy_key_2, :p_legacy_key_3,
			:s_date_created, :s_created_by, :s_date_modified, :s_modified_by, :__cx_osml_control,
			disp_name = condition(char_length(rtrim(:p_org_name)) > 0, :p_org_name + ' ' + condition(char_length(:p_given_name + :p_surname) > 0, '- ', ''), '') + isnull(:p_given_name + ' ','') + isnull(:p_surname + ' ','') + condition(isnull(:p_merged_with,'X') = :parameters:param_id, '(old; merged)', '') + condition(:p_partner_key = :parameters:param_merge, '(new; merged into)', ''),
			disp_ico = '/apps/kardia/images/icons/' + condition(:p_partner_class == 'IND', 'person.gif', 'group.gif')
		FROM
			/apps/kardia/data/Kardia_DB/p_partner/rows
		WHERE
			(:p_partner_key = :parameters:param_id or			-- the 'primary' record
			:p_partner_key = :parameters:param_parent or			-- the parent of the record
			isnull(:p_parent_key,'X') = :parameters:param_id or		-- children of the record
			isnull(:p_parent_key,'X') = :parameters:param_parent or		-- siblings (shared parent) of the record
			isnull(:p_merged_with,'X') = :parameters:param_id or		-- obsolete rec merged with this one
			:p_partner_key = :parameters:param_merge) and			-- this rec is obs; merged with record...
			convert(integer, :p_partner_key) > 0
		ORDER BY
			charindex('*' + rtrim(:p_partner_key) + '*', '*' + :parameters:param_id + '*') desc,
			charindex('*' + rtrim(:p_partner_key) + '*', '*' + :parameters:param_parent + '*') desc,
			charindex('*' + rtrim(:p_parent_key) + '*', '*' + :parameters:param_id + '*') desc,
			charindex('*' + rtrim(:p_parent_key) + '*', '*' + :parameters:param_parent + '*') desc,
			convert(integer,:p_partner_key)
		";
        baseobj = "/apps/kardia/data/Kardia_DB/p_partner/rows";
        replicasize=20;
        readahead=10;
        autoquery = never;

	param_id "widget/parameter" { type=string; default=null; }
	param_parent "widget/parameter" { type=string; default=null; }
	param_merge "widget/parameter" { type=string; default=null; }

	partner_sync "widget/rule"
	    {
	    ruletype = osrc_relationship;
	    target = primary_partner_osrc;
	    key_1 = param_id;
	    key_2 = param_parent;
	    key_3 = param_merge;
	    target_key_1 = p_partner_key;
	    target_key_2 = p_parent_key;
	    target_key_3 = p_merged_with;
	    }

        ConfirmWindow "widget/childwindow"
            {
            title = "i18n:Data Was Modified!";
            titlebar = yes;
            hdr_bgcolor="#c00000";
            bgcolor= "#e0e0e0";
            visible = false;
            style = dialog;
            x=200;y=200;width=300;height=140;

            warninglabel "widget/label"
                {
                x=10;y=10;width=276;height=30;
                text="i18n:Some data was modified.  Do you want to save it first, discard your modifications, or simply cancel the operation?";
                }

            _3bConfirmSave "widget/textbutton"
                {
                x=10;y=75;width=80;height=30;
                tristate=no;
                background="/sys/images/grey_gradient.png";
                text = "i18n:Save";
                fgcolor1=black;fgcolor2=white;
                }
            _3bConfirmDiscard "widget/textbutton"
                {
                x=110;y=75;width=80;height=30;
                tristate=no;
                background="/sys/images/grey_gradient.png";
                text = "i18n:Discard";
                fgcolor1=black;fgcolor2=white;
                }
            _3bConfirmCancel "widget/textbutton"
                {
                x=210;y=75;width=80;height=30;
                tristate=no;
                background="/sys/images/grey_gradient.png";
                text = "i18n:Cancel";
                fgcolor1=black;fgcolor2=white;
                }
            }

        p_partner_form "widget/form"
            {
            _3bconfirmwindow = "ConfirmWindow";
            bgcolor = "#e0e0e0";

	    p_partner_vbox "widget/vbox"
		{
		x=0;y=0;width=828;height=584;
		spacing=4;

		label_and_tools "widget/hbox"
		    {
		    height=20;
		    spacing=4;

		    partner_label "widget/label"
			{
			width=646;
			font_size=14;
			style=bold;
			fgcolor="#153f5f";
			
			value = runclient(condition(:p_partner_form:form_mode != 'NoData', condition(:p_partner_form:form_mode == 'New', '** Create New Partner **', condition(char_length(:p_partner_osrc:p_org_name) > 0, :p_partner_osrc:p_org_name + ' ' + condition(char_length(:p_partner_osrc:p_given_name + :p_partner_osrc:p_surname) > 0, '- ', ''), '') + isnull(:p_partner_osrc:p_title + ' ','') + isnull(:p_partner_osrc:p_given_name + ' ','') + isnull(:p_partner_osrc:p_surname + ' ','') + ' #' + :p_partner_osrc:p_partner_key + ''), '** None Selected **'));
			}

		    search_btn "widget/imagebutton"
			{
			y=1;
			width=18; height=18;
			image="/sys/images/ico21a.gif";
			pointimage="/sys/images/ico21b.gif";
			clickimage="/sys/images/ico21c.gif";
			disabledimage="/sys/images/ico21d.gif";
			tooltip = runserver("i18n:Search for Partners");
			enabled = runclient(:p_partner_form:is_queryable);

			search_cn "widget/connector" { event=Click; target=partner_search_window; action=Open; IsModal=1; }
			}

		    edit_btn "widget/component"
			{
			y=1;
			width=18; height=18;
			path = "/sys/cmp/edit_btn.cmp";
			}
		    save_btn "widget/component"
			{
			y=1;
			width=18; height=18;
			path = "/sys/cmp/save_btn.cmp";
			}
		    cancel_btn "widget/component"
			{
			y=1;
			width=18; height=18;
			path = "/sys/cmp/cancel_btn.cmp";
			}
		    formstat "widget/formstatus"
			{
			y=0;
			height=20; width=90;
			style=largeflat;
			form=p_partner_form;
			}
		    }

		sep_line "widget/pane"
		    {
		    height=5;
		    style=lowered;
		    background="/apps/kardia/images/bg/light_bgnd3.jpg";
		    }
		sep_line_bot "widget/autolayoutspacer" { height=4; }

		p_partner_hbox "widget/hbox"
		    {
		    height=140;
		    spacing=10;

		    fields_vbox "widget/vbox"
			{
			spacing=4;
			width=610;

			fields_twocol_hbox "widget/hbox"
			    {
			    height=116;
			    spacing=10;

			    p_partner_long_vbox "widget/vbox"
				{
				cellsize=20; spacing=4;
				width=350;
				p_title_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_title; text="i18n:Title:"; ctl_type="editbox"; form=p_partner_form; tooltip="i18n:The Partner's Title"; }
				p_given_name_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_given_name; text="i18n:First Name:"; lookup_width=305; ctl_type="editbox"; form=p_partner_form; tooltip="i18n:The first (Given) name of the partner"; }
				p_surname_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_surname; text="i18n:Last Name:"; lookup_width=305; ctl_type="editbox"; form=p_partner_form; tooltip="i18n:The last Name (Surname) of the partner"; }
				p_preferred_name_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_preferred_name; text="i18n:Preferred Name:"; lookup_width=305; ctl_type="editbox"; form=p_partner_form; tooltip="i18n:Preferred name (nickname, etc) of the partner"; }
				p_org_name_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_org_name; text="i18n:Organization:"; lookup_width=305; ctl_type="editbox"; form=p_partner_form; tooltip="i18n:The name of the organization"; }
				}
			    p_partner_short_vbox "widget/vbox"
				{
				cellsize=20; spacing=4;
				width=250;
				p_partner_key_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_partner_key; text="i18n:Partner ID:"; ctl_type="label"; form=p_partner_form; tooltip="i18n:This is the partner key assigned to each partner. It consists of the fund id followed by a computer generated six digit number."; }
				ckbox_hbox "widget/hbox"
				    {
				    spacing=10;
				    p_no_solicitations_cmp "widget/component" { width=130; label_width=103; path="/sys/cmp/smart_field.cmp"; field=p_no_solicitations; text="i18n:No Solicit.:"; ctl_type="checkbox"; form=p_partner_form; tooltip="i18n:Do not solicit from this partner" ; }
				    p_no_mail_cmp "widget/component" { width=80; label_width=60; path="/sys/cmp/smart_field.cmp"; field=p_no_mail; text="i18n:No Mail:"; ctl_type="checkbox"; form=p_partner_form; tooltip="i18n:Do not send any mail to this partner" ; }
				    }
				p_status_code_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; form=p_partner_form; field=p_status_code; text="i18n:Status:"; ctl_type="dropdown"; sql="select :text, :tag FROM /apps/kardia/data/Kardia_DB/_p_status_code/rows";}
				p_partner_class_dropdown "widget/component"
				    {
				    path="/sys/cmp/smart_field.cmp";
				    text="i18n:Partner Type:";
				    ctl_type=dropdown;
				    field = "p_partner_class";
				    form = p_partner_form;
				    tooltip="i18n:This defines what type of partner this is." ; 
				    sql="select :text, :tag from /apps/kardia/data/Kardia_DB/_p_partner_class/rows";
				    }
				p_gender_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; form=p_partner_form; field=p_gender; text="i18n:Gender:"; tooltip="i18n:The partners gender: M=Male F=Female C=Couple O=Other (for organizations)"; ctl_type="dropdown"; sql="SELECT :text, :tag FROM /apps/kardia/data/Kardia_DB/_p_partner_gender/rows";}
				}
			    }
			p_comments_cmp "widget/component" { height=20; label_width=100; path="/sys/cmp/smart_field.cmp"; field=p_comments; text="i18n:Comments:"; lookup_width=305; ctl_type="editbox"; form=p_partner_form; tooltip=""; }
			}
		    related_pane "widget/pane"
			{
			width=208;
			widget_class = "table_bgnd";
			related_table "widget/table"
			    {
			    x=0; y=0;
			    width=206;
			    height=138;
			    mode=dynamicrow;
			    rowheight = 20;
			    overlap_scrollbar = yes;
			    demand_scrollbar = yes;
			    colsep = 0;

			    ico "widget/table-column" { width=22; title = ""; fieldname = disp_ico; type=image; }
			    nm "widget/table-column" { width=188; title = "i18n:Related Partners:"; fieldname = disp_name; }
			    }
			}
                    }

//		sep_line2_top "widget/autolayoutspacer" { height=4; }
//		sep_line2 "widget/pane"
//		    {
//		    height=5;
//		    style=lowered;
//		    background="/apps/kardia/images/bg/light_bgnd3.jpg";
//		    }
		sep_line2_bot "widget/autolayoutspacer" { height=4; }

                Partner_MainTabPage "widget/tab"
                    {
                    height = 360;
                    //bgcolor="#d0d0d0";
                    selected = "partner_address_tab";
                    //selected = "person_relationship_tab";
		    background="/apps/kardia/images/bg/light_bgnd2.jpg";
		    inactive_background="/apps/kardia/images/bg/light_bgnd3.jpg";
                    partner_address_tab "widget/tabpage"
                        {
                        title = "i18n:Address/Contact";
                        textcolor = black;
                        //x=0; y=0; width=833; height=338;
            		person_location_cmp "widget/component"
                            {
                            path="/apps/kardia/modules/base/p_location.cmp";
                            x=10; y=10; width=806; height=338;
                            }
                        }
                    partner_rest_tab "widget/tabpage"
                        {
                        title = "i18n:Advanced...";
                        textcolor = black;
                        x=0; y=0; width=835; height=428;
			p_partner_adv_vbox1 "widget/vbox"
			    {
			    x=10;y=10;cellsize=20; spacing=4;
			    height=338;
			    width=300;

			    locale_hdr "widget/component" { path="/apps/kardia/modules/base/section_label.cmp"; text="i18n:National / Local Info"; }
			    p_language_code_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_language_code; text="i18n:Language:"; ctl_type="editbox"; form=p_partner_form; tooltip="i18n:The Partners Language"; }
			    localized_name_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_localized_name; text="i18n:Localized Name:"; ctl_type="editbox"; form=p_partner_form; tooltip="i18n:The name as written in the partner's national language"; }
			    p_surname_first_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_surname_first; text="i18n:Last Name First:"; ctl_type="checkbox"; form=p_partner_form; tooltip="i18n:Is the Last Name (Surname) listed first as in many countries"; }
			    sep00 "widget/autolayoutspacer" { height=4; }
			    legacy_hdr "widget/component" { path="/apps/kardia/modules/base/section_label.cmp"; text="i18n:Legacy System"; }
			    p_legacy_key_1_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_legacy_key_1; text="i18n:Legacy Key 1:"; ctl_type="editbox"; form=p_partner_form; tooltip="i18n:Legacy Key 1"; }
			    p_legacy_key_2_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_legacy_key_2; text="i18n:Legacy Key 2:"; ctl_type="editbox"; form=p_partner_form; tooltip="i18n:Legacy Key 2"; }
			    p_legacy_key_3_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_legacy_key_3; text="i18n:Legacy Key 3:"; ctl_type="editbox"; form=p_partner_form; tooltip="i18n:Legacy Key 3"; }
			    sep01 "widget/autolayoutspacer" { height=4; }
			    addl_hdr "widget/component" { path="/apps/kardia/modules/base/section_label.cmp"; text="i18n:Additional..."; }
			    p_best_contact_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_best_contact; text="i18n:Best Contact:"; ctl_type="editbox"; form=p_partner_form; tooltip="i18n:Best contact"; }
			    p_acquisition_code_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_acquisition_code; text="i18n:Acquisition:"; ctl_type="editbox"; form=p_partner_form; tooltip="i18n:How the partner was acquired"; }
			    }

			p_partner_adv_vbox2 "widget/vbox"
			    {
			    x=326; y=10; cellsize=20; spacing=4;
			    height=338;
			    width=300;
			    //p_cost_center_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_cost_center; text="i18n:Cost Center:"; ctl_type="editbox"; form=p_partner_form; tooltip="i18n:Cost Center associated with the partner" ; }
			    //sep01 "widget/autolayoutspacer" { height=4; }
			    recinfo_hdr "widget/component" { path="/apps/kardia/modules/base/section_label.cmp"; text="i18n:Record Information"; }
			    p_record_status_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; form=p_partner_form; field=p_record_status_code; text="i18n:Record Status:"; sql="select :text, :tag from /apps/kardia/data/Kardia_DB/_p_record_status/rows"; tooltip="i18n:The status of the record"; ctl_type="dropdown";}
			    p_status_change_date_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_status_change_date; text="i18n:Status Change:"; ctl_type="datetime"; form=p_partner_form; tooltip="i18n:Date of the last status change";}
			    p_creating_office_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_creating_office; text="i18n:Creating Office:"; ctl_type="editbox"; form=p_partner_form; tooltip="i18n:This is the office which created the record"; }
			    p_merged_with_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_merged_with; text="i18n:Merged With:"; ctl_type="editbox"; form=p_partner_form; tooltip="i18n:The partner this partner was merged with." ; }
			    p_parent_key_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=p_parent_key; text="i18n:Parent Key:"; ctl_type="editbox"; form=p_partner_form; tooltip="i18n:For hierarcies of various sorts"; }
			    s_date_created_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=s_date_created; text="i18n:Date Created:"; ctl_type="datetime"; type="create"; form=p_partner_form; tooltip="i18n:The date the record was created."; }
			    s_created_by_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=s_created_by; text="i18n:Created By:"; ctl_type="editbox"; type="create"; form=p_partner_form; tooltip="i18n:The user who created the partner record" ; }
			    s_date_modified_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=s_date_modified; text="i18n:Date Modified:"; ctl_type="datetime"; type="modify"; form=p_partner_form; tooltip="i18n:The date the record was modified."; }
			    s_modified_by_cmp "widget/component" { path="/sys/cmp/smart_field.cmp"; field=s_modified_by; text="i18n:Modified By:"; ctl_type="editbox"; type="modify"; form=p_partner_form; tooltip="i18n:The user who modified the partner record" ; }
			    }
                        }
                    person_class_tab "widget/tabpage"
                        {
                        title = "i18n:Person";
                        textcolor = black;
                        x=0; y=0; width=833; height=338;
			visible = runclient( :p_partner_class_dropdown:value == 'IND' );
                        person_person_cmp "widget/component"
                            {
                            path="/apps/kardia/modules/base/p_person.cmp";
                            x=0; y=0; width=831; height=338;
                            sync_osrc = p_partner_osrc;
                            }
			}
                    church_class_tab "widget/tabpage"
                        {
                        title = "i18n:Church";
                        textcolor = black;
                        x=0; y=0; width=833; height=338;
			visible = runclient( :p_partner_class_dropdown:value == 'CHU' );
                        person_church_cmp "widget/component"
                            {
                            path="/apps/kardia/modules/base/p_church.cmp";
                            x=0; y=0; width=831; height=338;
                            sync_osrc = p_partner_osrc;
                            }
                        }
                    person_relationship_tab "widget/tabpage"
                        {
                        title = "i18n:Relationships";
                        textcolor = black;
                        x=0; y=0; width=835; height=338;
                        person_relationship_cmp "widget/component"
                            {
                            path="/apps/kardia/modules/base/p_partner_relationship.cmp";
                            x=0; y=0; width=831; height=338;
                            sync_osrc = p_partner_osrc;
			    cnParterRelationshipSelected "widget/connector"
				{
				event="PartnerSelected";
				action="QueryParam";
				target="p_partner_osrc";
				//p_partner_key = runclient('100007');
				p_partner_key = runclient(:partner_key);
				}
                            }
			}

		    plugin_tabs "widget/repeat"
			{
			sql = "select path = :cx__pathname, module = :cx__pathpart4, component = :cx__pathpart5, :title from object wildcard '/apps/kardia/modules/*/plugin_base_partneredit_*.cmp'";

			plugin_tab "widget/tabpage"
			    {
			    title = runserver(:plugin_tabs:title);

			    plugin_cmp "widget/component"
				{
				height=338; width=808; x=10; y=10;
				path = runserver(:plugin_tabs:path);
				partner_osrc = p_partner_osrc;
				ledger = runserver(:this:ledger);
				}
			    }
			}
                    }
                }
            }
        }

    search_button "widget/textbutton"
        {
	condition = 0;
	x=740; y=199;
	height=24; width=100;
	text="i18n:Search";
	cnOpen "widget/connector" { event=Click; target=partner_search_window; action="Open"; IsModal=runclient(1);}
	}

    partner_search_window "widget/childwindow"
        {
        x=59;y=53;
        height=494; width=722;
        visible=false;
        toplevel = yes;
        style = dialog;
	bgcolor = "#e0e0e0";
	titlebar = no;
	modal = yes;
	//title = "i18n:Partner Search";
	//icon = "/apps/kardia/images/icons/person-search.gif";
        partner_search_component "widget/component"
            {
            x=10;y=10;
            height=472; width=700;
            path="/apps/kardia/modules/base/partner_search.cmp";
	    sync_osrc=p_partner_osrc;
	    sync_form=p_partner_form;
	    cnParterSelected "widget/connector"
	        {
	        event="PartnerSelected";
	        action="QueryParam";
	        target="primary_partner_osrc";
		p_partner_key = runclient(:partner_key);
	        }
	    cnCloseMe "widget/connector"
		{
		event="CloseMe";
		action=SetVisibility; 
		IsVisible=0;
		target="partner_search_window";
		}
            }
        }
    contact_search_button "widget/textbutton"
        {
	condition = 0;
	x=630; y=199;
	height=24; width=100;
        text="i18n:search contact";
        cnContactOpen "widget/connector" { event=Click; target=partner_contact_search_window; action="Open"; IsModal=runclient(1);}
        }

    partner_contact_search_window "widget/childwindow"
        {
        x=0;y=0;
        height=465; width=700;
        visible=false;
        toplevel = yes;
        style = dialog;
        bgcolor = "#e0e0e0";
        partner_contact_search_cmp "widget/component"
            {
            x=0;y=0;
            height=465; width=700;
            path="/apps/kardia/modules/base/partner_contact_search.cmp";
            sync_osrc=p_partner_osrc;
            sync_form=p_partner_form;
            cnParterContact_Selected "widget/connector"
                {
                event="PartnerSelected";
                action="QueryParam";
                target="p_partner_osrc";
                p_partner_key = runclient(:partner_key);
                }
            cnCloseMeContact "widget/connector"
                {
                event="CloseMe";
                action=SetVisibility;
                IsVisible=0;
                target="partner_contact_search_window";
                }
            }
	}
    }
