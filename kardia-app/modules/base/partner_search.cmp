$Version=2$
partner_search_component "widget/component-decl"
    {
    width=700;height=472;

    auto_search "widget/parameter"
	{
	//If a search query is specified during window opening...
	type=string;
	default=null;
	}
    sync_osrc "widget/parameter"
        {
        //This osrc is a parameter passed in from the outside
        type=object;
        find_container="widget/osrc";
        }
    sync_form "widget/parameter"
        {
        //This osrc is a parameter passed in from the outside
        type=object;
        find_container="widget/form";
        }

    window_container "widget/parameter"
	{
	type=object;
	find_container="widget/childwindow";
	}

    auto_focus "widget/connector"
	{
	source=window_container;
	event=Open;
	target=search_controls;
	action=SetFocus;
	}

    PartnerSelected "widget/component-decl-event" {  }
    PartnerNew "widget/component-decl-event" {  }
    PartnerCancel "widget/component-decl-event" {  }
    CloseMe "widget/component-decl-event" {  }

    OpenForms "widget/component-decl-action" {  }

    NextResult "widget/component-decl-action" { }
    PrevResult "widget/component-decl-action" { }

    NextResult_cn1 "widget/connector"
	{
	event=NextResult;
	target=person_search_osrc;
	action=Next;
	}
    NextResult_cn2 "widget/connector" 
	{ 
	condition=0;
	event=NextResult;
	target="partner_search_component"; 
	action="PartnerSelected"; 
	partner_key = runclient(:person_search_osrc:p_partner_key);
	}

    PrevResult_cn1 "widget/connector"
	{
	event=PrevResult;
	target=person_search_osrc;
	action=Prev;
	}
    PrevResult_cn2 "widget/connector" 
	{ 
	condition=0;
	event=PrevResult;
	target="partner_search_component"; 
	action="PartnerSelected"; 
	partner_key = runclient(:person_search_osrc:p_partner_key);
	}

    person_search_osrc "widget/osrc"
        {
        sql="	SELECT
			:l:p_address_3, :l:p_address_2, :l:p_location_type, :l:p_country_code, :l:p_address_1,
			:l:p_postal_code, :l:p_state_province, :l:p_in_care_of, :l:p_city, :l:p_location_id,
			:p:p_partner_key, :p:p_partner_class, :p:p_title, :p:p_given_name, :p:p_preferred_name,
			:p:p_surname, :p:p_org_name, :p:p_comments,  :p:p_legacy_key_1, :p:p_legacy_key_2, :p:p_legacy_key_3,
			disp_name = condition(char_length(rtrim(:p:p_org_name)) > 0, :p:p_org_name + ' ' + condition(char_length(:p:p_given_name + :p:p_surname) > 0, '- ', ''), '') + isnull(:p:p_given_name + ' ','') + isnull(:p:p_surname + ' ',''),
			disp_addr = condition(char_length(:l:p_address_1) > 0, :l:p_address_1 + ' ', '') + condition(char_length(:l:p_address_2) > 0, :l:p_address_2 + ' ', '') + condition(char_length(:l:p_address_3) > 0, :l:p_address_3 + ' ', ''),
			disp_city = condition(char_length(:l:p_city) > 0, :l:p_city + ', ', '') + condition(char_length(:l:p_state_province) > 0, :l:p_state_province + ' ', ''),
			is_payee = isnull(:y:p_allow_payments,0),
			is_donor = isnull(:d:p_allow_contributions,0)
		FROM
			/apps/kardia/data/Kardia_DB/p_partner/rows p,
			/apps/kardia/data/Kardia_DB/p_location/rows l,
			/apps/kardia/data/Kardia_DB/p_donor/rows d,
			/apps/kardia/data/Kardia_DB/p_payee/rows y
		WHERE
			:p:p_partner_key = :l:p_partner_key and
			:p:p_record_status_code != 'X' and
			:l:p_revision_id = 0 and
			:d:p_partner_key =* :p:p_partner_key and
			:y:p_partner_key =* :p:p_partner_key
		";
        baseobj = "/apps/kardia/data/Kardia_DB/p_partner/rows";
        replicasize=50;
        readahead=25;
        autoquery=never;

	sel_partner_if_window_closed "widget/connector"
	    {
	    event=DataFocusChanged;
	    event_condition=runclient(not :window_container:is_visible);
	    target="partner_search_component"; 
	    action="PartnerSelected"; 
	    partner_key = runclient(:person_search_osrc:p_partner_key);
	    }

	search_controls "widget/component"
	    {
	    path = "partner_search_controls.cmp";
	    x=0; y=0; width=700; height=225;
	    auto_search=runserver(:this:auto_search);
	    }

	search_tabs "widget/tab"
	    {
	    condition=0;
	    x=0; y=0;
	    width=700; height=200;
	    background="/apps/kardia/images/bg/light_bgnd2.jpg";
	    inactive_background="/apps/kardia/images/bg/light_bgnd3.jpg";

	    express_tab "widget/tabpage"
		{
		title = "Express Search";

		express_vbox "widget/vbox"
		    {
		    x=8; y=8; width=682; height=182;
		    spacing=4;
		    
		    inst_lbl "widget/label"
			{
			height = 40;
			text = "Enter parts of the name, -or- parts of the address, and then press [ENTER] or click the Search button.  You don't need to type the entire name - the first few letters will do.  For instance, to search for John Smith, you can enter 'J Smi' and all names that are similar to J Smi will be listed.";
			}
		    sp1 "widget/autolayoutspacer" { height=4; }

		    name_eb_lbl "widget/label"
			{
			style=bold;
			height=16;
			text = "Name / ID:";
			}
		    name_eb "widget/editbox"
			{
			height=20;
			width=370;
			empty_description = "Type all or part of the Name or ID, and press ENTER";

			do_name_search "widget/connector"
			    {
			    event=ReturnPressed;
			    event_condition=runclient(char_length(:name_eb:content) > 0);
			    target=person_search_osrc;
			    action=QueryText;
			    cx__case_insensitive=1;
			    //field_list="*p_given_name*,p_surname*,*p_org_name*,*p_preferred_name*,p_partner_key,*p_comments*,p_legacy_key_1,*p_legacy_key_2*";
			    field_list="*p_given_name*,*p_surname*,*p_org_name*,*p_preferred_name*,p_partner_key,*p_comments*,p_legacy_key_1,*p_legacy_key_2*,p_legacy_key_3";
			    objname=runclient("p");
			    query=runclient(:name_eb:content);
			    }
			}
		    sp2 "widget/autolayoutspacer" { height=4; }
		    addr_eb_lbl "widget/label"
			{
			style=bold;
			height=16;
			text = "Address:";
			}
		    addr_eb "widget/editbox"
			{
			height=20;
			width=370;
			empty_description = "Type all or part of the Addr/City/St, and press ENTER";

			do_addr_search "widget/connector"
			    {
			    event=ReturnPressed;
			    event_condition=runclient(char_length(:addr_eb:content) > 0);
			    target=person_search_osrc;
			    action=QueryText;
			    cx__case_insensitive=1;
			    //field_list="*p_address_1*,*p_address_2*,*p_address_3*,*p_in_care_of*,p_city*,p_state_province,p_postal_code*,p_partner_key,*p_location_comments*";
			    field_list="*p_address_1*,*p_address_2*,*p_address_3*,*p_in_care_of*,p_city*,p_state_province,p_postal_code*,p_partner_key";
			    objname=runclient("l");
			    query=runclient(:addr_eb:content);
			    }
			}
		    sp3 "widget/autolayoutspacer" { height=4; }
		    search_btn "widget/textbutton"
			{
			width=100;
			text="Search";

			do_btn_name_search "widget/connector"
			    {
			    event=Click;
			    event_condition=runclient(not (char_length(:addr_eb:content) > 0) or char_length(:name_eb:content) > 0);
			    target=person_search_osrc;
			    action=QueryText;
			    cx__case_insensitive=1;
			    //field_list="*p_given_name*,p_surname*,*p_org_name*,*p_preferred_name*,p_partner_key,*p_comments*,p_legacy_key_1,*p_legacy_key_2*";
			    field_list="*p_given_name*,*p_surname*,*p_org_name*,*p_preferred_name*,p_partner_key,*p_comments*,p_legacy_key_1,*p_legacy_key_2*,p_legacy_key_3";
			    objname=runclient("p");
			    query=runclient(:name_eb:content);
			    }
			do_btn_addr_search "widget/connector"
			    {
			    event=Click;
			    event_condition=runclient(char_length(:addr_eb:content) > 0 and not (char_length(:name_eb:content) > 0));
			    target=person_search_osrc;
			    action=QueryText;
			    cx__case_insensitive=1;
			    //field_list="*p_address_1*,*p_address_2*,*p_address_3*,*p_in_care_of*,p_city*,p_state_province,p_postal_code*,p_partner_key,*p_location_comments*";
			    field_list="*p_address_1*,*p_address_2*,*p_address_3*,*p_in_care_of*,p_city*,p_state_province,p_postal_code*,p_partner_key";
			    objname=runclient("l");
			    query=runclient(:addr_eb:content);
			    }
			}
		    }
		}

	    advanced_tab "widget/tabpage"
		{
		title = "Advanced Search";

		partner_search_form "widget/form"
		    {
		    allow_nodata = no;
		    allow_view = no;
		    allow_modify = no;
		    allow_new = no;
		    auto_focus = no;

		    advinst_lbl "widget/label"
			{
			x=8; y=8; width=682;
			height = 30;
			text = "Type your search and press ENTER.  You can use '*' as a wildcard.  For example, enter 'J*' in First Name to search for all first names that start with J.  Enter '*son' in Last Name to find all last names that end with 'son'.";
			}
		    advanced_hbox "widget/hbox"
			{
			x=8; y=48; width=682; height=142;

			first_vbox "widget/vbox"
			    {
			    width=225;
			    cellsize=20;
			    spacing=4;

			    p_partner_key_cmp "widget/component" { type="readonly"; path="/sys/cmp/smart_field.cmp"; field=p_partner_key; text="Partner ID:"; ctl_type="editbox"; tooltip="Partner Key"; }
			    p_given_name_cmp "widget/component" { type="readonly"; path="/sys/cmp/smart_field.cmp"; field=p_given_name; text="First Name:"; lookup_width=325; ctl_type="editbox"; tooltip="The first (Given) name of the partner"; }
			    p_preferred_name_cmp "widget/component" { type="readonly"; path="/sys/cmp/smart_field.cmp"; field=p_preferred_name; text="Salutation:"; lookup_width=325; ctl_type="editbox"; tooltip="Preferred name (nickname, etc) of the partner"; }
			    p_surname_cmp "widget/component" { type="readonly"; path="/sys/cmp/smart_field.cmp"; field=p_surname; text="Last Name:"; lookup_width=325; ctl_type="editbox"; tooltip="The last Name (Surname) of the partner"; }
			    p_org_name_cmp "widget/component" { type="readonly"; path="/sys/cmp/smart_field.cmp"; field=p_org_name; text="Organization:"; lookup_width=325; ctl_type="editbox"; tooltip="The name of the organization"; }
			    p_in_care_of_cmp "widget/component" { type="readonly"; path="/sys/cmp/smart_field.cmp"; field=p_in_care_of; text="In care of:"; lookup_width=325; ctl_type="editbox"; tooltip="In Care Of"; }
			    }
			second_vbox "widget/vbox"
			    {
			    width=225;
			    cellsize=20;
			    spacing=4;

			    p_address_1_cmp "widget/component" { type="readonly"; path="/sys/cmp/smart_field.cmp"; field=p_address_1; text="Addr Line 1:"; lookup_width=325; ctl_type="editbox"; tooltip="First Address"; }
			    p_address_2_cmp "widget/component" { type="readonly"; path="/sys/cmp/smart_field.cmp"; field=p_address_2; text="Addr Line 2:"; lookup_width=325; ctl_type="editbox"; tooltip="Second Address"; }
			    p_address_3_cmp "widget/component" { type="readonly"; path="/sys/cmp/smart_field.cmp"; field=p_address_3; text="Addr Line 3:"; lookup_width=325; ctl_type="editbox"; tooltip="Third Address field for when address 1 and address2 are filled"; }
			    p_city_cmp "widget/component" { type="readonly"; path="/sys/cmp/smart_field.cmp"; field=p_city; text="City:"; lookup_width=305; ctl_type="editbox"; tooltip="The city or township"; }
			    p_state_province_cmp "widget/component" { type="readonly"; path="/sys/cmp/smart_field.cmp"; field=p_state_province; text="State/Prov:"; lookup_width=305; ctl_type="editbox"; tooltip="Use country-keyed ref tbl 4 state/prov/county"; }
			    p_postal_code_cmp "widget/component" { type="readonly"; path="/sys/cmp/smart_field.cmp"; field=p_postal_code; text="Postal Code:"; ctl_type="editbox"; tooltip="zip code or postal code"; }
			    }
			third_vbox "widget/vbox"
			    {
			    width=225;
			    cellsize=20;
			    spacing=4;

			    p_country_code_cmp "widget/component" { type="readonly"; path="/sys/cmp/smart_field.cmp"; field=p_country_code; text="Country Code:"; lookup_width=305; ctl_type="editbox"; tooltip="Use ISO codes same as domain names"; }
			    p_partner_class_cmp "widget/component" { type="readonly"; path="/sys/cmp/smart_field.cmp"; field=p_partner_class; text="Partner Type:"; lookup_width=305; tooltip=""; ctl_type="dropdown"; sql="select :text, :tag from /apps/kardia/data/Kardia_DB/_p_partner_class/rows";}
			    p_title_cmp "widget/component" { type="readonly"; path="/sys/cmp/smart_field.cmp"; field=p_title; text="Title:"; ctl_type="editbox"; tooltip="The Partners Title"; }
			    p_comments_cmp "widget/component" { type="readonly"; path="/sys/cmp/smart_field.cmp"; field=p_comments; text="Comments:"; lookup_width=305; ctl_type="editbox"; tooltip=""; }
			    p_legacy_key_1_cmp "widget/component" { type="readonly"; path="/sys/cmp/smart_field.cmp"; field=p_legacy_key_1; text="Legacy Key:"; ctl_type="editbox"; tooltip="Legacy Key 1"; }
			    p_legacy_key_2_cmp "widget/component" { type="readonly"; path="/sys/cmp/smart_field.cmp"; field=p_legacy_key_2; text="Legacy Key 2:"; ctl_type="editbox"; tooltip="Legacy Key 2"; }
			    }
			}
		    }
		}
	    }

	results_pane "widget/pane"
	    {
	    width=700;height=205;
	    x=0;y=233;
	    widget_class = "table_bgnd";

	    results_table "widget/table"
		{
		x=0;y=0;
		height=203;
		width=698;
		mode=dynamicrow;

		cnPartnerSelected "widget/connector" 
		    { 
		    event=DblClick;
		    target="partner_search_component"; 
		    action="PartnerSelected"; 
		    partner_key = runclient(:person_search_osrc:p_partner_key);
		    }
		cnPartnerSelectedClose "widget/connector" 
		    { 
		    event=DblClick;
		    target="window_container"; 
		    action="Close"; 
		    }

		table_key "widget/table-column" { title = "ID"; fieldname = p_partner_key; width=60; }
		//table_firstname "widget/table-column" { title = "First Name"; fieldname = p_given_name; }
		//table_lastname "widget/table-column" { title = "Last Name"; fieldname = p_surname; }
		table_name "widget/table-column" { title = "Name"; fieldname = disp_name; width = 185; }
		//table_address1 "widget/table-column" { title = "Address 1"; fieldname = p_address_1; }
		//table_address2 "widget/table-column" { title = "Address 2"; fieldname = p_address_2; }
		//table_address3 "widget/table-column" { title = "Address 3"; fieldname = p_address_3; }
		table_address "widget/table-column" { title = "Address"; fieldname = disp_addr; width = 185; }
		//table_city "widget/table-column" { title = "City"; fieldname = p_city; }
		//table_state "widget/table-column" { title = "State"; fieldname = p_state_province; }
		table_city "widget/table-column" { title = "City"; fieldname = disp_city; width = 105; }
		table_zip "widget/table-column" { title = "Zip/Postal"; fieldname = p_postal_code; width=83; }
		//table_legacy1 "widget/table-column" { title = "Legacy 1"; fieldname = p_legacy_key_1; width=50; }
		//table_legacy2 "widget/table-column" { title = "Legacy 2"; fieldname = p_legacy_key_2; width=75; }
		table_isdnr "widget/table-column" { title = "D"; fieldname = is_donor; width=20; type=check; }
		table_ispay "widget/table-column" { title = "P"; fieldname = is_payee; width=20; type=check; }
		}
	    }

	btn_hbox "widget/hbox"
	    {
	    width=700; height=24;
	    x=0; y=448;
	    align=center;
	    spacing=8;

	    select_btn "widget/textbutton"
		{
		text = "Select";
		width = 100;
		height = 24;
		enabled = runclient(char_length(:person_search_osrc:p_partner_key) > 0);
		cnSelect "widget/connector" 
		    { 
		    event=Click;
		    target="partner_search_component"; 
		    action="PartnerSelected"; 
		    partner_key = runclient(:person_search_osrc:p_partner_key);
		    }
		cnSelect2 "widget/connector" 
		    { 
		    event=Click;
		    target="window_container"; 
		    action="Close"; 
		    }
		}
	    cancel_btn "widget/textbutton"
		{
		text = "Cancel";
		width = 100;
		height = 24;
		cnCancel2 "widget/connector" 
		    { 
		    event=Click;
		    target="window_container"; 
		    action="Close"; 
		    }
		cnCancel "widget/connector" 
		    { 
		    event=Click;
		    target="partner_search_component"; 
		    action="PartnerCancel"; 
		    }
		}
	    new_btn "widget/textbutton"
		{
		text = "Create New";
		width = 100;
		height = 24;
		cnNew "widget/connector" 
		    { 
		    event=Click;
		    target="partner_search_component"; 
		    action="PartnerNew"; 
		    }
		cnNew2 "widget/connector" 
		    { 
		    event=Click;
		    target="window_container"; 
		    action="Close"; 
		    }
		}
	    }
	}
    }
