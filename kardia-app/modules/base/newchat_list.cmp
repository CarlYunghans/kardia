// This proivdes a list of all the chats that a person is currently in
$Version=2$
newchat_list "widget/component-decl"
    {
    width=282; height=150;
    
    // Parameters
    shows_invited "widget/parameter" {type="integer"; default=1;}
    shows_current "widget/parameter" {type="integer"; default=1;}
    shows_exited "widget/parameter" {type="integer"; default=0;}
    with_who "widget/parameter" {type="string"; default=runserver(user_name());}
    refreshes "widget/parameter" {type="integer";}
    
    decline_confirmation "widget/parameter" {type="object";}
    chat_window "widget/parameter" {type="object";}
    
    refresh_timer "widget/timer"
	{
	msec=30000;
	auto_reset=0;
	interval_cn "widget/connector" { target=chat_list_osrc; event=Expire; action=Refresh;}
	}
	
    // The data source for listing chats
    // Need to add 
    chat_list_osrc "widget/osrc"
	{
	sql=runserver("   select
		    :member:c_status,
		    :member:c_chat_id,
		    :member:c_last_read_message_id,
		    :chat:c_chat_id,
		    :chat:c_title,
		    :chat:c_last_message_id,
		    status= condition(:member:c_status == 'O', condition(:member:c_last_read_message_id < :chat:c_last_message_id, '/apps/kardia/images/icons/chat_stat_open_new.png', '/apps/kardia/images/icons/chat_stat_open_no_new.png'),
			    condition(:member:c_status == 'C', condition(:member:c_last_read_message_id < :chat:c_last_message_id, '/apps/kardia/images/icons/chat_stat_closed_new.png', '/apps/kardia/images/icons/chat_stat_closed_no_new.png'),
			    '/apps/kardia/images/icons/chat_stat_invite.png')),
		    chat=condition(:member:c_status == 'I', '/sys/images/green_check.gif','/apps/kardia/images/icons/chat.png'),
		    close=" + runserver(condition(:shows_exited, "condition(:member:c_status != 'I', '/sys/images/trans_1.gif', '/sys/images/red_x.gif')", "'/sys/images/red_x.gif'")) + "
		from
		    /apps/kardia/data/Kardia_DB/c_member/rows member,
		    /apps/kardia/data/Kardia_DB/c_chat/rows chat
		where
		    :member:c_chat_id == :chat:c_chat_id " + 
		    runserver(condition(:shows_exited, "", ", and :member:c_status != 'C'")) + "
		");
		
	replicasize=100;
	readahead=100;
	autoquery=onfirstreveal;
	endquery_cn "widget/connector" { target=refresh_timer; event=EndQuery; action=SetTimer; Time=30000; }
	indicates_activity = no;
	
	chat_list_table "widget/table"
	    {
	    width=282; height=150;
	    rowheight = 20;
	    mode=dynamicrow;
	    allow_selection = "yes";
	    show_selection = no;
	    demand_scrollbar = yes;
	    overlap_scrollbar = yes;
	    colsep = 0;
	    
	    cn_chat_window "widget/connector"
		{
		event="Click";
		event_condition=runclient(:Column == 'chat');
		target=chat_window;
		action="Instantiate";
		
		// Parameters to pass
		chat_id = 0;
		    
		} // End cn_chat_window
		
	    cn_decline_invitation "widget/connector"
		{
		event="Click";
		event_condition=runclient(:Column == "close" and :c_status == "I");
		target=decline_confirmation;
		action="Instantiate";
		
		// Parameters
		chat_name=runclient(:c_title + " ");
		}
		
	    cn_close_open_chat_invitation "widget/connector"
		{
		event="Click";
		event_condition=runclient(:Column == "close" and :c_status == "O");
		target=chat_list_osrc;
		action="Modify";
		
		// Parameters
		c_status=runclient("C");
		}
	    
	    status_indicator "widget/table-column" {title=""; fieldname="status";width=24; type="image";}
	    member_col "widget/table-column" {title="Chat Name"; fieldname="c_title"; width=180;}
	    chat_image "widget/table-column" {title=""; fieldname="chat"; type="image"; width=24;}
	    close_image "widget/table-column" {title="";fieldname="close";type="image";width=24;}
	    } // End chat_list_table
	} // End chat_list_osrc
    } // End newchat_list