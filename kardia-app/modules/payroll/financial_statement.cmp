$Version=2$
financial_statement_cmp "widget/component-decl"
    {
    width=778; height=525;

    ledger "widget/parameter" { type=string; default=null; allowchars="0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ"; }

    page "widget/parameter" { type=object; find_container="widget/page"; }
    fswin "widget/parameter" { type=object; find_container="widget/childwindow"; }

    ledger_osrc "widget/osrc"
	{
	l_ledger "widget/parameter" { type=string; default=runserver(:this:ledger); }
	sql = runserver("select * from /apps/kardia/data/Kardia_DB/a_ledger/rows where :a_ledger_number = :parameters:l_ledger");
	baseobj = "/apps/kardia/data/Kardia_DB/a_ledger/rows";
	replicasize=10;
	readahead=5;
	autoquery = onload;
	}

    report_form "widget/form"
	{
	allow_nodata = no;
	allow_new = no;
	auto_focus = no;
	allow_obscure = yes;

	vb1 "widget/vbox"
	    {
	    x=8;y=8;width=762;height=207;
	    fl_height = 0;
	    spacing=4;
	    lbl_opt "widget/label" { height=30; font_size=16; text="i18n:Missionary Financial Statement Options:"; align=center; }

	    pn_sep1 "widget/pane" { height=2; style=lowered; }

	    f_ledger "widget/component" { width=350; height=24; path="/sys/cmp/smart_field.cmp"; field='ledger'; ctl_type=label; text='Ledger:'; value=runserver(:this:ledger); form=report_form; label_width=120; }
	    f_costctr "widget/component" { width=350; height=24; path="/apps/kardia/modules/base/editbox_tree.cmp"; field="costctr"; popup_source=runserver("/apps/kardia/modules/gl/costctrs.qyt/" + :this:ledger + "/"); popup_text="Choose Cost Center:"; text="Cost Center:"; attach_point=editbox; empty_desc = "required"; label_width=120; }
	    f_year "widget/component"
		{ 
		width=350; height=24; 
		path="/sys/cmp/smart_field.cmp"; 
		field='year_period'; 
		ctl_type=dropdown; 
		text="i18n:Year (req'd):"; 
		sql = runserver("select :a_period + ' - ' + :a_period_desc, :a_period from /apps/kardia/data/Kardia_DB/a_period/rows where :a_ledger_number = " + quote(:this:ledger) + " and :a_parent_period is null order by :a_period desc");
		form=report_form;
		label_width=120;

		year_sel_cn_1 "widget/connector"
		    {
		    event="DataChange";
		    event_condition=runclient(char_length(:report_form:year_period) > 0);
		    target=f_startperiod;
		    action=SetSource;
		    Source = runclient("/apps/kardia/modules/gl/periods.qyt/" + :report_form:ledger + "/" + :report_form:year_period + "|" + :report_form:ledger + "/");
		    }
		}
	    f_startperiod "widget/component" { width=350; height=24; path="/apps/kardia/modules/base/editbox_tree.cmp"; field="period"; popup_source=runserver("/apps/kardia/modules/gl/periods.qyt/" + :this:ledger + "/"); popup_text="i18n:Period:"; text="i18n:Statement Period:"; attach_point=editbox; empty_desc = "i18n:required"; label_width=120; }
	    f_unposted "widget/component" { x=10; width=400; height=24; path="/sys/cmp/smart_field.cmp"; field="unposted"; ctl_type='checkboxleft'; text="i18n:Include unposted transactions"; form=report_form; label_width=120; }
	    }

	rpt_print "widget/textbutton"
	    {
	    x=650; y=52; width=120; height=24;
	    text="i18n:Print";
	    //enabled=runclient(char_length(:f_year:value) > 0 and char_length(:report_form:period) > 0 and char_length(:report_form:costctr) > 0);
	    rpt_print_cn "widget/connector" { event="Click"; target="report_form"; action="Submit"; Target=runclient("page"); NewPage=runclient(1); Source=runclient("/apps/kardia/modules/payroll/financial_statement.rpt"); Width=runclient(800); Height=runclient(600); UseragentResize=runclient(1); UseragentMenu=runclient(1); }
	    }
	rpt_cancel "widget/textbutton"
	    {
	    x=650; y=80; width=120; height=24;
	    text="i18n:Close";
	    rpt_cancel_cn "widget/connector" { event="Click"; target="fswin"; action="Close"; }
	    }
	}

    missionary_osrc "widget/osrc"
	{
	sql = "SELECT :c:a_cost_center, :c:a_cc_desc, cctxt = :c:a_cost_center + ' - ' + :c:a_cc_desc, paynames = sum(:y:a_payee_name + ',') - ',', paycnt = count(:y:a_payroll_id)  FROM IDENTITY /apps/kardia/data/Kardia_DB/a_cost_center/rows c, /apps/kardia/data/Kardia_DB/a_payroll/rows y  WHERE :c:a_cost_center = :c:a_bal_cost_center and :c:a_cost_center_class = 'MIS' and :c:a_ledger_number *= :y:a_ledger_number and :c:a_cost_center *= :y:a_cost_center  GROUP BY :c:a_cost_center";
	baseobj = "/apps/kardia/data/Kardia_DB/a_cost_center/rows";
	autoquery=never;
	replicasize=150;
	readahead=25;

	ledger_sync "widget/rule"
	    {
	    ruletype = "osrc_relationship";
	    target = ledger_osrc;
	    is_slave = yes;
	    key_1 = a_ledger_number;
	    target_key_1 = a_ledger_number;
	    revealed_only = true;
	    }

	dfc_cn "widget/connector"
	    {
	    event=DataFocusChanged;
	    action=SetValue;
	    target=f_costctr;
	    Value = runclient(:missionary_osrc:a_cost_center);
	    }
	dfc_cn2 "widget/connector"
	    {
	    event=DataFocusChanged;
	    action = SetValueDescription;
	    target=f_costctr;
	    Description = runclient(:missionary_osrc:a_cc_desc);
	    }

	payee_form "widget/form"
	    {
	    allow_modify = no;
	    allow_new = no;
	    allow_delete = no;

	    payee_ctls "widget/component"
		{
		x=8;y=215;width=762;height=26;
		path = "/sys/cmp/form_controls.cmp";
		deletable = no;
		object_name = "Fund";
		}
	    }
	payee_list_pane "widget/pane"
	    {
	    x=8;y=241;width=762;height=272;
	    widget_class = "table_bgnd";

	    payee_list "widget/table"
		{
		x=0;y=0;width=760;height=270;
		rowheight = 18;
		mode=dynamicrow;
		fl_height = 100;

		t_cc "widget/table-column" { title="i18n:Cost Center / Fund"; fieldname="cctxt"; width=300; }
		t_paycnt "widget/table-column" { title="i18n:# Payees"; fieldname="paycnt"; width=40; }
		t_paynames "widget/table-column" { title="i18n:Payroll Names"; fieldname="paynames"; width=420; }
		}
	    }
	}
    }
